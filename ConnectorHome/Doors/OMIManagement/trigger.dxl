pragma encoding, "UTF-8"
noError()
Module orct_mCurrent = current
if (null orct_mCurrent) halt
Project orct_pCurrent = getParentProject(orct_mCurrent)
if (null orct_pCurrent) halt
string orct_ConfigFolder = fullName(orct_pCurrent) "/Config/"
if (matches(orct_ConfigFolder, fullName(orct_mCurrent))) halt
Module orct_cacheModule
string orct_nameOfCacheModule = fullName(orct_pCurrent) "/Orchestra Configuration/Orchestra Cache " getDatabaseIdentifier() "." uniqueID(orct_pCurrent)
if (fullName(orct_mCurrent) == orct_nameOfCacheModule) halt

orct_cacheModule = read(orct_nameOfCacheModule, false, true)
if (null orct_cacheModule) halt
current = orct_mCurrent

string orct_currentModuleId = uniqueID(module(fullName(orct_mCurrent)))
AttrDef orct_attributDefinition = find (orct_mCurrent, "Orchestra Module Id")
bool orct_attributeCreated = false
string orct_IdForCreation = ( getDatabaseIdentifier ).."."..orct_currentModuleId
if (null orct_attributDefinition && canCreate(orct_mCurrent)) {
	orct_attributDefinition = create module type "string" attribute "Orchestra Module Id" default orct_IdForCreation""
	orct_attributeCreated = true
}
if (null orct_attributDefinition) {
	if (open module orct_nameOfCacheModule){close(orct_cacheModule)}
	halt
}
if (orct_attributeCreated) {
	save orct_mCurrent
}

string orct_currentOrchestraID = orct_mCurrent."Orchestra Module Id"

if (length(orct_currentOrchestraID) == 0 || !matches("\\.", orct_currentOrchestraID)) {
	if (!isEdit orct_mCurrent) halt
	orct_mCurrent."Orchestra Module Id" = orct_IdForCreation
	orct_currentOrchestraID = orct_IdForCreation
	save orct_mCurrent
}

Object orct_objectForModule
bool orct_foundModule = false
string orct_doorsIdInCache
int orct_absoluteNumber
if (!orct_attributeCreated) {
	int orct_recordCount = 0
	int orct_deletionCount = 0
	int orct_currentAbsoluteNumber
	string orct_OrchestraIDInCache
	Skip orct_SkipObjectToDelete = create

	for orct_objectForModule in entire(orct_cacheModule) do {
		if (isDeleted(orct_objectForModule)) {
			continue
		}
		orct_OrchestraIDInCache = orct_objectForModule."ModuleIdOrchestra"
		orct_doorsIdInCache = orct_objectForModule."ModuleIdDoors"
		orct_currentAbsoluteNumber = orct_objectForModule."Absolute Number"
		if (!null orct_OrchestraIDInCache && !null orct_doorsIdInCache) {
			if (orct_OrchestraIDInCache == orct_currentOrchestraID) {
				if (orct_doorsIdInCache == orct_currentModuleId) {
					orct_recordCount++
					if (orct_recordCount > 1) {
						put(orct_SkipObjectToDelete, orct_currentAbsoluteNumber, orct_currentAbsoluteNumber)
						orct_deletionCount++
					}
				}
				if (!orct_foundModule) {
					orct_foundModule = true
					orct_absoluteNumber = orct_currentAbsoluteNumber
				}
			} else if (orct_doorsIdInCache == orct_currentModuleId) {
				put(orct_SkipObjectToDelete, orct_currentAbsoluteNumber, orct_currentAbsoluteNumber)
				orct_deletionCount++
			}
		}
	}
	if (orct_deletionCount > 0) {
		if (open module orct_nameOfCacheModule){close(orct_cacheModule)}
		orct_cacheModule = edit(orct_nameOfCacheModule, false, true, true)
		if (null orct_cacheModule) halt
		for orct_currentAbsoluteNumber in orct_SkipObjectToDelete do {
			orct_objectForModule = object(orct_currentAbsoluteNumber, orct_cacheModule)
			if (null orct_objectForModule) {
				continue
			}
			softDelete(orct_objectForModule)
		}
		purgeObjects_(orct_cacheModule)
		save(orct_cacheModule)
	}
	delete(orct_SkipObjectToDelete)
}
if (orct_foundModule) {
	orct_objectForModule = object(orct_absoluteNumber, orct_cacheModule)
	orct_doorsIdInCache = orct_objectForModule."ModuleIdDoors"
	if (orct_doorsIdInCache == orct_currentModuleId) {
		if (open module orct_nameOfCacheModule){
			close(orct_cacheModule)
		}
		halt
	}
	if (open module orct_nameOfCacheModule){close(orct_cacheModule)}
	orct_cacheModule = edit(orct_nameOfCacheModule, false, true, true)
	if (null orct_cacheModule) halt
	current = orct_mCurrent
	orct_objectForModule = object(orct_absoluteNumber, orct_cacheModule)
	if (null orct_objectForModule) {
		if (open module orct_nameOfCacheModule){save(orct_cacheModule); close(orct_cacheModule)}
		halt
	}
	if (null itemFromID orct_doorsIdInCache) {
		orct_objectForModule."ModuleIdDoors" = orct_currentModuleId
	} else {
		if (!isEdit orct_mCurrent) halt
		orct_mCurrent."Orchestra Module Id" = orct_IdForCreation
		save orct_mCurrent
		orct_objectForModule = create(orct_cacheModule)
		orct_objectForModule."ModuleIdDoors" = orct_currentModuleId
		orct_objectForModule."ModuleIdOrchestra" = orct_IdForCreation
	}
} else {
	if (open module orct_nameOfCacheModule){
		close(orct_cacheModule)
	}
	orct_cacheModule = edit(orct_nameOfCacheModule, false, true, true)
	if (null orct_cacheModule) halt
	current = orct_mCurrent
	orct_objectForModule = create(orct_cacheModule)
	orct_objectForModule."ModuleIdDoors" = orct_currentModuleId
	orct_objectForModule."ModuleIdOrchestra" = orct_currentOrchestraID
}

if (open module orct_nameOfCacheModule){save(orct_cacheModule); close(orct_cacheModule)}
















